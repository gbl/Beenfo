buildscript {
    dependencies {
        classpath 'de.guntram.mcmod:crowdin-translate:1.4+1.19-pre2'
    }
    repositories {
        maven {
            name = 'CrowdinTranslate source'
            url = "https://minecraft.guntram.de/maven/"
        }
    }
}

plugins {
	id 'fabric-loom' version '0.12-+'
	id "com.modrinth.minotaur" version "2.+"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: 'de.guntram.mcmod.crowdin-translate'
crowdintranslate.crowdinProjectName = 'beenfo'

repositories {
	maven {
		url = "https://maven.fabricmc.net/"
	}
        maven {
                url = "https://minecraft.guntram.de/maven/"
        }
	maven {
		url = "https://maven.terraformersmc.com/releases/"
	}
        mavenLocal()        // This needs to be filled with Spigot by running java -jar BuildTools.jar --rev 1.19 --remapped
}

sourceCompatibility = 17
targetCompatibility = 17

ext.Versions = new Properties()
Versions.load(file("Versionfiles/mcversion-22w45a.properties").newReader())

archivesBaseName = "beenfo"
ext.projectVersion = "1.3.3"

version = "${Versions['minecraft_version']}-fabric${Versions['fabric_versiononly']}-${project.projectVersion}"

loom {
    mixin.defaultRefmapName = "Beenfo-refmap.json";
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

dependencies {
    
    // This sucks, but the spigot server part needs Craft... classes which
    // aren't in spigots maven, so if you want to compile the mod, you need
    // to get a spigot.jar first. Sorry.

    minecraft  "com.mojang:minecraft:${Versions['minecraft_version']}"
    mappings   "net.fabricmc:yarn:${Versions['yarn_mappings']}:v2"
    modImplementation "net.fabricmc:fabric-loader:${Versions['loader_version']}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${Versions['fabric_version']}"
    modImplementation "com.terraformersmc:modmenu:${Versions['modmenu_version']}"
    modImplementation "de.guntram.mcmod:crowdin-translate:${Versions['crowdintranslate_version']}"
    include "de.guntram.mcmod:crowdin-translate:${Versions['crowdintranslate_version']}"
    modImplementation "de.guntram.mcmod:GBfabrictools:${Versions['gbfabrictools_version']}"
    include    "de.guntram.mcmod:GBfabrictools:${Versions['gbfabrictools_version']}"
    implementation     ("org.spigotmc:spigot:1.19.1-R0.1-SNAPSHOT:remapped-mojang")         // see above, need to compile spigot to have this available
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

build {
    dependsOn downloadTranslations
}

jar {
    from "LICENSE"
}

// WARNING
// The following must be ran for the spigot part to work.  Unfortunately,
// my gradle-fu is not strong enough to automate this.
// For some reason, building spigot did not generate the shaded SpecialSource,
// so I got it from https://repo.maven.apache.org/maven2/net/md-5/SpecialSource/1.10.0/SpecialSource-1.10.0-shaded.jar.
// This is explained in https://www.spigotmc.org/threads/spigot-bungeecord-1-17-1-17-1.510208/



// export VERSION=1.19.1
// export PLUGIN=build/libs/beenfo-$VERSION-fabric0.58.5-1.3.3
// java17 -cp $HOME/.m2/repository/net/md-5/SpecialSource/1.10.0/SpecialSource-1.10.0-shaded.jar:$HOME/.m2/repository/org/spigotmc/spigot/$VERSION-R0.1-SNAPSHOT/spigot-$VERSION-R0.1-SNAPSHOT-remapped-mojang.jar net.md_5.specialsource.SpecialSource --live -i $PLUGIN.jar -o $PLUGIN-obf.jar -m $HOME/.m2/repository/org/spigotmc/minecraft-server/$VERSION-R0.1-SNAPSHOT/minecraft-server-$VERSION-R0.1-SNAPSHOT-maps-mojang.txt --reverse
// java17 -cp $HOME/.m2/repository/net/md-5/SpecialSource/1.10.0/SpecialSource-1.10.0-shaded.jar:$HOME/.m2/repository/org/spigotmc/spigot/$VERSION-R0.1-SNAPSHOT/spigot-$VERSION-R0.1-SNAPSHOT-remapped-obf.jar net.md_5.specialsource.SpecialSource --live -i $PLUGIN-obf.jar -o $PLUGIN-spigot.jar -m $HOME/.m2/repository/org/spigotmc/minecraft-server/$VERSION-R0.1-SNAPSHOT/minecraft-server-$VERSION-R0.1-SNAPSHOT-maps-spigot.csrg


import com.modrinth.minotaur.TaskModrinthUpload

modrinth {
    projectId = 'beenfo'
    versionName = project.archivesBaseName
    uploadFile = remapJar // This is the java jar task
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN") ?: "0"
    project {
        id = '357256'
        releaseType = 'release'
        addGameVersion("${Versions['minecraft_version']}")
        addGameVersion("Java "+targetCompatibility)
        addGameVersion("Fabric")
        mainArtifact(remapJar)
    }
    options {
        forgeGradleIntegration = false
    }
}
